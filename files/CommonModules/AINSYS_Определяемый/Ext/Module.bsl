// Вызывается из правила Товары в Демо базе
Процедура ДобавитьСодержимоеДокументацииВВыборку(ЭтоПравило, ВыборкаВыгрузки, Параметры) Экспорт 
	// добавим содержимое файлов в выборку
	Для Каждого стрТовары Из ВыборкаВыгрузки.Строки Цикл
		спрОбъект = стрТовары.Ссылка.ПолучитьОбъект();
		поз = 0;
		Для Каждого стрДокументация из стрТовары.Строки Цикл
			ДвоичныеДанныеДокументация = спрОбъект.Документация[поз].ФайлСДокументацией.Получить();
			стрДокументация.СодержимоеФайла = Base64Строка(ДвоичныеДанныеДокументация);
			поз = поз + 1;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Процедура ДополнитьВыборкуКонтрагенты(ЭтоПравило, ВыборкаВыгрузки, Параметры) Экспорт
	масКонтрагентов = Новый Массив;
	Для Каждого стрДерева из ВыборкаВыгрузки.Строки Цикл
		масКонтрагентов.Добавить(стрДерева.Ссылка);
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗаказыКлиентовОстатки.ЗаказКлиента.Контрагент КАК Контрагент,
	|	ЗаказыКлиентовОстатки.ЗаказКлиента КАК Заказ,
	|	ЗаказыКлиентовОстатки.СуммаЗаказаОстаток КАК СуммаЗаказа
	|ИЗ
	|	РегистрНакопления.ЗаказыКлиентов.Остатки(, ЗаказКлиента.Контрагент В (&масКонтрагентов)) КАК ЗаказыКлиентовОстатки
	|ИТОГИ
	|	СУММА(СуммаЗаказа)
	|ПО
	|	Контрагент";
	Запрос.УстановитьПараметр("масКонтрагентов", масКонтрагентов);
	ВыборкаКонтрагентов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	// колонку ОткрытыеЗаказы можно также добавить в СКД, а тут переопределить
	// но и так тоже можно	
	ВыборкаВыгрузки.Колонки.Добавить("ОткрытыеЗаказы");
	Для Каждого стрДерева из ВыборкаВыгрузки.Строки Цикл
		ВыборкаКонтрагентов.Сбросить();
		Если НЕ ВыборкаКонтрагентов.НайтиСледующий(стрДерева.Ссылка, "Контрагент") Тогда
			Продолжить
		КонецЕсли;
		
		// ради этого пример и пишется.
		// в дерево значений после работы СКД добавляем свои данные 
		стрДерева.ОткрытыеЗаказы = Новый Структура("СуммаЗаказов, Заказы", ВыборкаКонтрагентов.СуммаЗаказа, Новый Массив);
		ВыборкаЗаказов = ВыборкаКонтрагентов.Выбрать();
		Пока ВыборкаЗаказов.Следующий() Цикл
			стркЗаказ = Новый Структура("Заказ, Сумма", ВыборкаЗаказов.Заказ, ВыборкаЗаказов.СуммаЗаказа);
			стрДерева.ОткрытыеЗаказы.Заказы.Добавить(стркЗаказ);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

